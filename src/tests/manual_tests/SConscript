"""
Copyright (c) 2016 Doyub Kim
"""

Import('env', 'os', 'utils')

script_dir = os.path.dirname(File('SConscript').rfile().abspath)

def from_root_dir(path):
	return os.path.join(*[script_dir, '../../..', path])

def from_build_dir(path):
	return os.path.join(app_env['BUILDDIR'], path)

app_env = env.Clone()
app_env.Append(CPPPATH = [from_root_dir('include'), from_root_dir('external/src/cnpy'), from_root_dir('external/src/gtest/googletest/googletest/include'), from_root_dir('external/src/pystring'), script_dir])
app_env.Append(LIBPATH = [from_build_dir('src/jet'), from_build_dir('external/src/cnpy'), from_build_dir('external/src/gtest'), from_build_dir('external/src/obj'), from_build_dir('external/src/pystring')])
app_env.Prepend(LIBS = ['jet', 'cnpy', 'gtest', 'obj', 'pystring'])

# TODO: This is BAD! Should be fixed from cnpy
app_env.Append(CXXFLAGS=['-Wno-sign-compare'])

source_patterns = ['*.cpp']
source = map(lambda x: os.path.relpath(x, script_dir), utils.get_all_files(script_dir, source_patterns))

app = app_env.Program('manual_tests', source)

Return('app_env', 'app')

